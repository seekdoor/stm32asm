	@ stm32f030f4p6 asm
	@ lvdt实验
	 .thumb
	.syntax unified
.section .data
zhengxian1k:
	.short 0x78,0x7c,0x80,0x83,0x87,0x8b,0x8e,0x92,0x96,0x99,0x9d,0xa1,0xa4,0xa8,0xab,0xae,0xb2,0xb5,0xb8,0xbb,0xbf,0xc2,0xc4,0xc7,0xca,0xcd,0xcf,0xd2,0xd4,0xd7,0xd9,0xdb,0xdd,0xdf,0xe1,0xe3,0xe5,0xe6,0xe8,0xe9,0xea,0xeb,0xec,0xed,0xee,0xef,0xef,0xef,0xf0,0xf0,0xf0,0xf0,0xf0,0xef,0xef,0xef,0xee,0xed,0xec,0xeb,0xea,0xe9,0xe8,0xe6,0xe5,0xe3,0xe1,0xdf,0xdd,0xdb,0xd9,0xd7,0xd4,0xd2,0xcf,0xcd,0xca,0xc7,0xc4,0xc2,0xbf,0xbb,0xb8,0xb5,0xb2,0xae,0xab,0xa8,0xa4,0xa1,0x9d,0x99,0x96,0x92,0x8e,0x8b,0x87,0x83,0x80,0x7c,0x78,0x74,0x70,0x6d,0x69,0x65,0x62,0x5e,0x5a,0x57,0x53,0x4f,0x4c,0x48,0x45,0x42,0x3e,0x3b,0x38,0x35,0x31,0x2e,0x2c,0x29,0x26,0x23,0x21,0x1e,0x1c,0x19,0x17,0x15,0x13,0x11,0xf,0xd,0xb,0xa,0x8,0x7,0x6,0x5,0x4,0x3,0x2,0x1,0x1,0x1,0x0,0x0,0x0,0x0,0x0,0x1,0x1,0x1,0x2,0x3,0x4,0x5,0x6,0x7,0x8,0xa,0xb,0xd,0xf,0x11,0x13,0x15,0x17,0x19,0x1c,0x1e,0x21,0x23,0x26,0x29,0x2c,0x2e,0x31,0x35,0x38,0x3b,0x3e,0x42,0x45,0x48,0x4c,0x4f,0x53,0x57,0x5a,0x5e,0x62,0x65,0x69,0x6d,0x70,0x74,0x78
lcdshuju:
	.ascii "yjmwxwx-20180225"
	
	.equ STACKINIT,         0x20001000
	.equ asciimabiao,	0x20000000
	.equ dianyabiao,	0x20000010
.section .text
xiangliangbiao:        
        .word STACKINIT         
        .word _start + 1        
        .word _nmi_handler + 1  
        .word _hard_fault  + 1  
        .word 0 
        .word 0    
        .word 0
	.word 0
	.word 0
	.word 0
	.word 0
	.word _svc_handler +1
	.word 0 
	.word 0
	.word _pendsv_handler +1
	.word _systickzhongduan + 1
_start:
_shizhong:
	ldr r2, = 0x40022000   @FLASH访问控制
	movs r1, # 0x32
	str r1, [r2]
	ldr r0, = 0x40021000 @ rcc
	@0x34时钟控制寄存器 2 (RCC_CR2)
	movs r1, # 0x01
	str r1, [r0, # 0x34]  @ HSI开14M时钟
_dengdai14mshizhongwending:
	ldr r1, [r0, # 0x34]
	lsls r1, r1, # 30     @ 左移30位
	bpl _dengdai14mshizhongwending  @ 等待14M时钟稳定

_neicunqingling:
	ldr r0, = 0x20000000
	movs r1, # 0
	ldr r3, = 0x1000
_neicunqinglingxunhuan:
	subs r3, # 4
	str r1, [r0, r3]
	bne _neicunqinglingxunhuan

_waisheshizhong:			 @ 外设时钟
	@+0x14=RCC_AHBENR
	@0=DMA @2=SRAM @4=FLITF@6=CRC @17=PA @18=PB @19=PC @20=PD @22=PF
	ldr r0, = 0x40021000
	ldr r1, = 0x460005
	str r1, [r0, # 0x14]

	@+0x18外设时钟使能寄存器 (RCC_APB2ENR)
	@0=SYSCFG @5=USART6EN @9=ADC @11=TIM1 @12=SPI1 @14=USART1 @16=TIM15 @17=TIM16 @18=TIM17 @22=DBGMCU
	ldr r1, = 0xa00
	str r1, [r0, # 0x18]
	@+0X1C=RCC_APB1ENR
	@1=TIM3 @4=TIM6 @5=TIM7 @8=TIM14 @11=WWDG @14=SPI @17=USRT2 @18=USART3
	@20=USART5 @21=I2C1 @22=I2C2 @23=USB @28=PWR

io_she_zhi:
	@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
	@a(0x48000000)b(0x48000400)c(0x48000800)d(0x48000c00)f(0x48001400)
	@ 输入（00），通用输出（01），复用功能（10），模拟（11）
	@偏移0x4 = 端口输出类型 @ （0 推挽），（ 1 开漏）
	@偏移0x8 = 输出速度  00低速， 01中速， 11高速
	@偏移0xC = 上拉下拉 (00无上下拉，  01 上拉， 10下拉)
	@偏移0x10 = 输入数据寄存器
	@偏移0x14 = 输出数据寄存器
	@偏移0x18 = 端口开  0-15置位
	@偏移0x28 = 端口关
	@0X20 = 复用低
	@GPIO口0（0-3位）每个IO口占用4位
	@ AF0 = 0X0000, AF1 = 0X0001, AF2 = 0X0010 AF3 = 0X0011, AF4 = 0X0100
	@ AF5 = 0X0101, AF6 = 0X0111, AF7 = 0X1000
	@0x24 = 复用高
	@GPIO口8 （0-3位）每个IO口占用4位
	@ AF0 = 0X0000, AF1 = 0X0001, AF2 = 0X0010 AF3 = 0X0011, AF4 = 0X0100
	@ AF5 = 0X0101, AF6 = 0X0111, AF7 = 0X1000
	@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

	ldr r0, = 0x48000000
	ldr r1, = 0x28005550
	str r1, [r0]

	ldr r1, = 0x200
	str r1, [r0, # 0x24]

dmachushihua:
	@+0=LSR,+4=IFCR,
	@+8=CCR1,+c=CNDTR1,+10=CPAR1+14=CMAR1,
	@+1c=CCR2,+20=CNDTR2,+24=CPAR2,+28=CMAR2
	@+30=CCR3,+34=CNDTR3,+38=CPAR2,+3c=CMAR3
	@+44=CCR4,+48=CNDTR4,+4c=CPAR4,+50=CMAR4
	@+58=CCR5,+5c=CNDTR5,+60=CPAR5,+64=CMAR5
	@+6C=CCR6,+70=CNDTR6,+74=CPAR6,+78=CMAR6
	@+80=CCR7,+84=CNDTR7,+88=CPAR7,+8c=CMAR7
	@ tim1DMA
	ldr r0, = 0x40020000
	ldr r1, = 0x40012c3c @ 外设地址
	str r1, [r0, # 0x60]
	ldr r1, = zhengxian1k @ 储存器地址
	str r1, [r0, # 0x64]
	ldr r1, = 200             @点数
	str r1, [r0, # 0x5c]
	ldr r1, = 0x25b1         @ 储存到外设
	str r1, [r0, # 0x58]
	@ adc dma
	ldr r1, = 0x40012440
	str r1, [r0, # 0x10]
	ldr r1, = dianyabiao
	str r1, [r0, # 0x14]
	ldr r1, =  0xff
	str r1, [r0, # 0x0c]
	ldr r1, = 0xa81
	str r1, [r0, # 0x08]


tim1chushiha:
	ldr r0, = 0x40012c00 @ tim1_cr1
	movs r1, # 0
	str r1, [r0, # 0x28] @ psc
	ldr r1, = 240
	str r1, [r0, # 0x2c] @ ARR
	ldr r1, = 0x68
	str r1, [r0, # 0x1c] @ ccmr2  CC3
	ldr r1, = 0x100    @  CC3
	str r1, [r0, # 0x20] @ ccer
	ldr r1, = 0x8000
	str r1, [r0, # 0x44] @ BDTR
	ldr r1, = 0x800 @ CC3 DMA
	str r1, [r0, # 0x0c] @ DIER
	ldr r1, = 0xe1
	str r1, [r0]

_adcchushihua:
	ldr r0, = 0x40012400  @ adc基地址
	ldr r1, = 0x80000000
	str r1, [r0, # 0x08]  @ ADC 控制寄存器 (ADC_CR)  @adc校准
_dengadcjiaozhun:
	ldr r1, [r0, # 0x08]
	 movs r1, r1
	bmi _dengadcjiaozhun   @ 等ADC校准
_kaiadc:
	ldr r1, [r0, # 0x08]
	movs r2, # 0x01
	orrs r1, r1, r2
	str r1, [r0, # 0x08]
_dengdaiadcwending:
	ldr r1, [r0]
	lsls r1, r1, # 31
	bpl _dengdaiadcwending @ 等ADC稳定
_tongdaoxuanze:
	ldr r1, = 0x01
	str r1, [r0, # 0x28]    @ 通道选择寄存器 (ADC_CHSELR)
	ldr r1, = 0x3003        @ 13 连续转换
	str r1, [r0, # 0x0c]    @ 配置寄存器 1 (ADC_CFGR1)
	movs r1, # 0x07         @
	str r1, [r0, # 0x14]    @ ADC 采样时间寄存器 (ADC_SMPR)
	ldr r1, [r0, # 0x08]
	ldr r2, = 0x04         @ 开始转换
	orrs r1, r1, r2
	str r1, [r0, # 0x08]    @ 控制寄存器 (ADC_CR)


	
_lcdchushihua:
	movs r0, # 0x33
	movs r1, # 0
	bl _xielcd
	bl _lcdyanshi
	movs r0, # 0x32
	movs r1, # 0
	bl _xielcd
	bl _lcdyanshi
	movs r0, # 0x28
	movs r1, # 0
	bl _xielcd
	bl _lcdyanshi
	movs r0, # 0x0c
	movs r1, # 0
	bl _xielcd
	bl _lcdyanshi
	movs r0, # 0x01
	movs r1, # 0
	bl _xielcd
	bl _lcdyanshi

	movs r0, # 0x80
	ldr r1, = lcdshuju
	movs r2, # 16
	bl _lcdxianshi

	

	
tingting:
	ldr r0, = 0x40012440
	ldr r0, [r0]
	movs r1, # 8
	ldr r2, = asciimabiao
	bl _zhuanascii

	movs r0, # 0xc0
	ldr r1, = asciimabiao
	movs r2, # 8
	bl _lcdxianshi
	
	
	b tingting

_lcdxianshi:  		@r0=LCD位置，r1=数据地址，r2=长度
	push {r0-r4,lr}
	mov r4, r1
	
	movs r1, # 0	
	bl _xielcd
	
	movs r1, # 1
	movs r3, # 0
_lcdxianshixunhuan:
	ldrb r0, [r4,r3]
	bl _xielcd
	adds r3, r3, # 1
	cmp r3, r2
	bne _lcdxianshixunhuan
	pop {r0-r4,pc}
	
_lcdyanshi:
	push {r5,lr}
	ldr r5, = 0x5ff
_lcdyanshixunhuan:
	subs r5, r5, # 1
	bne _lcdyanshixunhuan
	pop {r5,pc}
	
_xielcd:		@入R0=8位,r1=0命令,r1=1数据
	push {r0-r7,lr}
	lsrs r6, r0, # 4
	lsls r0, r0, # 28
	lsrs r0, r0, # 28
	movs r2, # 0x80		@ RS
	movs r3, # 0x40		@ E
	movs r5, # 0x3c
	ldr r4, = 0x48000000
	cmp r1, # 0
	beq _lcdmingling
	str r2, [r4, # 0x18]	@RS=1
	b _lcdshuju
_lcdmingling:
	str r2, [r4, # 0x28]	@RS=0
_lcdshuju:
	str r3, [r4, # 0x18]	@E=1
	str r5, [r4, # 0x28]

	lsls r7, r6, # 31
	lsrs r7, r7, # 26
	str r7, [r4, # 0x18]
	
	lsrs r7, r6, # 1
	lsls r7, r7, # 31
	lsrs r7, r7, # 27
	str r7, [r4, # 0x18]
	
	lsrs r7, r6, # 2
	lsls r7, r7, # 31
	lsrs r7, r7, # 28
	str r7, [r4, # 0x18]
	
	lsrs r7, r6, # 3
	lsls r7, r7, # 31
	lsrs r7, r7, # 29
	str r7, [r4, # 0x18]
	
	bl _lcdyanshi
	str r3, [r4, # 0x28]	@E=0


	str r3, [r4, # 0x18]    @E=1
	str r5, [r4, # 0x28]

	lsls r7, r0, # 31
	lsrs r7, r7, # 26
	str r7, [r4, # 0x18]

	lsrs r7, r0, # 1
	lsls r7, r7, # 31
	lsrs r7, r7, # 27
	str r7, [r4, # 0x18]

	lsrs r7, r0, # 2
	lsls r7, r7, # 31
	lsrs r7, r7, # 28
	str r7, [r4, # 0x18]

	lsrs r7, r0, # 3
	lsls r7, r7, # 31
	lsrs r7, r7, # 29
	str r7, [r4, # 0x18]

	bl _lcdyanshi
	str r3, [r4, # 0x28]    @E=0
	
	pop {r0-r7,pc}
	.ltorg


_zhuanascii:				@ 16进制转数码管码
		@ R0要转的数据， R1长度，R2结果表首地址
	push {r0-r7,lr}
	ldr r7, = asciimabiao
	mov r5, r0
	mov r6, r1
	movs r1, # 10
_xunhuanqiuma:
	bl _chufa
	mov r4, r0
	muls r4, r1
	subs r3, r5, r4
	adds r3, r3, # 0x30
	mov r5, r0
	subs r6, r6, # 1
	beq _qiumafanhui
	strb r3, [r2,r6]
	b _xunhuanqiuma
_qiumafanhui:
	strb r3, [r2, r6]
	pop {r0-r7,pc}
	
_chufa:				@软件除法
	@ r0 除以 r1 等于 商(r0)余数R1
	push {r1-r4,lr}
	cmp r0, # 0
	beq _chufafanhui
	cmp r1, # 0
	beq _chufafanhui
	mov r2, r0
	movs r3, # 1
	lsls r3, r3, # 31
	movs r0, # 0
	mov r4, r0
_chufaxunhuan:
	lsls r2, r2, # 1
	adcs r4, r4, r4
	cmp r4, r1
	bcc _chufaweishubudao0
	adds r0, r0, r3
	subs r4, r4, r1
_chufaweishubudao0:
	lsrs r3, r3, # 1
	bne _chufaxunhuan
_chufafanhui:
	pop {r1-r4,pc}
	.ltorg

_nmi_handler:
	bx lr
_hard_fault:
	bx lr
_svc_handler:
	bx lr
_pendsv_handler:
	bx lr
_systickzhongduan:
	bx lr
